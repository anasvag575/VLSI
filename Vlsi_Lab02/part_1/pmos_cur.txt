*Current PMOS characteristics
.INCLUDE "../MOSIS_025m.txt"

M1 d g s s CMOSP L=2u W=3u

Vsg s g DC 0
Vsd s d DC 0
Vd d 0 DC 0

*Setting up environment 
.control
set hcopydevtype=postscript
set hcopypscolor=true
set color0=rgb:f/f/f
set color1=rgb:0/0/0

run
*Normal current characteristics
DC Vsd 0 2.5 0.25 Vsg 0 2.5 0.25 

plot -i(vsd)
hardcopy pmos_cur.ps -i(vsd)

*Loop where we do dc analysis for every vgs
*value and calculate R_instant-R_eq_av
*VT of PMOS is -0.5536085
foreach i 0.8 1.2 1.8 2.5
  alter Vsg=$i

  if $i = 0.8
    DC Vsd 0.25 2.5 0.001
  end
  if $i = 1.2
    DC Vsd 0.65 2.5 0.001
  end
  if $i = 1.8
    DC Vsd 1.25 2.5 0.001
  end
  if $i = 2.5
    DC Vsd 1.95 2.5 0.001
  end

let insta_resist=-s/i(vsd)
let average_resist = mean(insta_resist)
print average_resist
plot insta_resist average_resist title 'Vsg=$i' 
end

.endc

.end
